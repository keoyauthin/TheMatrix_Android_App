/*
 * Copyright 2013 Wastl Kraus <derdoktor667@gmail.com>
 *
 *     Licensed under the Apache License, Version 2.0 (the "License");
 *     you may not use this file except in compliance with the License.
 *     You may obtain a copy of the License at
 *
 *         http://www.apache.org/licenses/LICENSE-2.0
 *
 *     Unless required by applicable law or agreed to in writing, software
 *     distributed under the License is distributed on an "AS IS" BASIS,
 *     WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *     See the License for the specific language governing permissions and
 *     limitations under the License.
 */

apply plugin: 'android'

dependencies {
    // ActionBarCompat
    compile 'com.android.support:appcompat-v7:19.+'

    // Support Libraries
    compile 'com.android.support:support-v4:19.+'

    // Google Play Services
    compile 'com.google.android.gms:play-services:3.+'

    // The libs Folder
    compile fileTree(dir: 'libs', includes: ['*.jar'])

    // Dropbox Chooser Library
    compile 'com.dropbox.chooser:android:1.+'

    // The Facebook SDK Library
    compile 'com.facebook.android:facebook:3.+'
}

android {
    compileSdkVersion "Google Inc.:Google APIs:19"
    buildToolsVersion "19.0.0"

    defaultConfig {
        minSdkVersion 8
        targetSdkVersion 19

        if (project.hasProperty('versionNumber') &&
        project.hasProperty('versionRevision')) {
            versionName project.versionNumber
            versionCode Integer.parseInt(project.versionRevision)
        }
    }

    if (project.hasProperty('signingStoreLocation') &&
    project.hasProperty('signingStorePassword') &&
    project.hasProperty('signingKeyAlias') &&
    project.hasProperty('signingKeyPassword')) {

        println ""
        println "Found sign properties in gradle.properties! Signing buildâ€¦"
        println ""

        signingConfigs {

            release {
                storeFile file(signingStoreLocation)
                storePassword signingStorePassword
                keyAlias signingKeyAlias
                keyPassword signingKeyPassword
            }

            debug {
                storeFile file(signingStoreLocation)
                storePassword signingStorePassword
                keyAlias signingKeyAlias
                keyPassword signingKeyPassword
            }
                    
            beta {
                storeFile file(signingStoreLocation)
                storePassword signingStorePassword
                keyAlias signingKeyAlias
                keyPassword signingKeyPassword
            }
        }

        buildTypes {

            release {
                debuggable false
                zipAlign true
                signingConfig signingConfigs.release
            }

            debug {
                versionNameSuffix " Debug "
                debuggable true
                zipAlign true
                signingConfig signingConfigs.debug
            }
                 
            beta {
                versionNameSuffix " Beta "
                debuggable true
                zipAlign true
                signingConfig signingConfigs.beta
            }
        }
    }
}
