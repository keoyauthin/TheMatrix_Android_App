/*
 * Copyright 2013 Wastl Kraus <derdoktor667@gmail.com>
 *
 *     Licensed under the Apache License, Version 2.0 (the "License");
 *     you may not use this file except in compliance with the License.
 *     You may obtain a copy of the License at
 *
 *         http://www.apache.org/licenses/LICENSE-2.0
 *
 *     Unless required by applicable law or agreed to in writing, software
 *     distributed under the License is distributed on an "AS IS" BASIS,
 *     WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *     See the License for the specific language governing permissions and
 *     limitations under the License.
 */

apply plugin: 'android'

repositories {
    maven {
        url 'https://github.com/derdoktor667/mvn-repo/raw/master/'
    }
    mavenCentral()

}

android {
    compileSdkVersion 18
    buildToolsVersion "18.1.0"

    def Properties versionProps = new Properties()
    versionProps.load new FileInputStream(file("../version.properties"))

    defaultConfig {
        minSdkVersion 8
        targetSdkVersion 18

        versionName versionProps.getAt('name')
        versionCode versionProps.getAt('code').toInteger()
    }

    dependencies {
        compile 'com.android.support:appcompat-v7:18.+'
        compile 'com.android.support:support-v4:18.+'
        compile 'com.google.android.gms:play-services:3.+'
        compile 'com.facebook.android:facebook:3.+'
    }

    signingConfigs {
        release {
            def Properties localProps = new Properties()
            localProps.load new FileInputStream(file('../local.properties'))
            def Properties keyProps = new Properties()
            assert localProps.getAt('keystore.props.file');
            keyProps.load new FileInputStream(file(localProps.getAt('keystore.props.file')))
            storeFile file(keyProps.getAt("store"))
            keyAlias keyProps.getAt("alias")
            storePassword keyProps.getAt("storePass")
            keyPassword keyProps.getAt("pass")
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
        }

        publicBeta.initWith(buildTypes.release)
        publicBeta {
            versionNameSuffix " Beta " + versionProps.getAt('betaNumber')
        }
    }
}
